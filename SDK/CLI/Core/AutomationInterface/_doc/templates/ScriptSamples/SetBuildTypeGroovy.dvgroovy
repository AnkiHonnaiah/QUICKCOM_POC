import com.vector.cfg.model.mdf.ar4x.adaptiveplatform.applicationdesign.applicationstructure.MIExecutable
import com.vector.cfg.model.mdf.ar4x.adaptiveplatform.platformmoduledeployment.adaptivemoduleimplementation.MIBuildTypeEnum

import static com.vector.cfg.automation.api.ScriptApi.daVinci

//daVinci enables the IDE code completion support
daVinci {
    scriptDescription "This script grabs all Executables in the model and sets the buildType to RELEASE."

     /*
     * Task: SetBuildTypeGroovy
     * Type: DV_PROJECT
     * -------------------------------------------------------------------------------------------------------
     * This script grabs all executables in the model and sets the buildType to RELEASE.
     * For more details please refer to section "Model" in the Automation Documentation.
     * -------------------------------------------------------------------------------------------------------
     */
    scriptTask("SetBuildTypeGroovy", DV_PROJECT){

        taskDescription 'Finds all Executables in the model and changes the buildType attribute.'

        taskHelp '''SetBuildType script task
                    This task finds all the Executables in the model and changes the buildType attribute.'''
        code {
            def logger = scriptLogger
            logger.info "SetBuildType execution started."

            // Start a transaction that will modify the model.
            transaction{
                // Find all elements of type Executable
                mdfModel(MIExecutable) { exec ->
                    // Every MIExecutable's buildType will be set to BUILD_TYPE_RELEASE
                    if (exec.buildType == MIBuildTypeEnum.BUILD_TYPE_RELEASE) {
                        logger.info "buildType of $exec is already BUILD_TYPE_RELEASE"
                    } else {
                        exec.buildType = MIBuildTypeEnum.BUILD_TYPE_RELEASE
                        logger.info "buildType of $exec changed to BUILD_TYPE_RELEASE"
                    }
                }
            }

            // In case the project shall be saved, uncomment the line below.
            // saveProject()
            logger.info "SetBuildType execution ended."
        }
    }
}
